buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'de.undercouch:gradle-download-task:4.1.1'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply from: 'gradle/scripts/setup.gradle'
apply from: 'gradle/scripts/binpatches.gradle'

version = VERSION_MAJOR+'.'+VERSION_MINOR+(VERSION_REVISION != '0' ? '.'+VERSION_REVISION : '')+(!VERSION_CLASSIFIER.isEmpty() ? '-'+VERSION_CLASSIFIER : '')
group = "mods.su5ed"
archivesBaseName = "ic2patcher"

sourceCompatibility = targetCompatibility = '1.8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"

    mappings = "snapshot_20171003"
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
}

dependencies {
    compile group: "net.industrial-craft", name: "industrialcraft-2", version: "${VERSION_IC2}-ex112", classifier: 'dev'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task releaseJar (type: Jar) {
    classifier ''
    dependsOn 'genBinIC2Patches'

    from (project.tasks.getByName('genBinIC2Patches').outputs.files)
    from sourceSets.main.output

    manifest {
        attributes "FMLCorePlugin": "mods.su5ed.ic2patcher.asm.PatcherFMLPlugin", "FMLCorePluginContainsFMLMod": true
    }
}

reobf {
    releaseJar {}
}

build {
    dependsOn 'releaseJar'
}

jar {
    dependsOn 'genBinIC2DevPatches'
    from (project.tasks.getByName('genBinIC2DevPatches').outputs.files)
    manifest {
        attributes "FMLCorePlugin": "mods.su5ed.ic2patcher.asm.PatcherFMLPlugin", "FMLCorePluginContainsFMLMod": true
    }
}

runClient {
    project(':IndustrialCraft2').tasks.each {it.onlyIf { false }}
}

runServer {
    project(':IndustrialCraft2').tasks.each {it.onlyIf { false }}
}
